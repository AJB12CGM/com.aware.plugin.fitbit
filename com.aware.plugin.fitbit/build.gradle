apply plugin: 'com.android.application'
apply plugin: 'com.jfrog.bintray'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.aware.plugin.fitbit" //EDIT ME
        minSdkVersion 10
        targetSdkVersion 25
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = android.defaultConfig.applicationId
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/sklakegg/aware-fitbit' //EDIT ME
        issueTrackerUrl = 'https://github.com/sklakegg/aware-fitbit/issues' //EDIT ME
        labels = ['aar','android','framework','aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/sklakegg/aware-fitbit.git' //EDIT ME
        desc = "AWARE Plugin: FitBit" //EDIT ME
        userOrg = user
        githubRepo = "sklakegg/aware-fitbit" //EDIT ME

        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

repositories {
    maven { url 'https://jitpack.io' } //this allows JIT library generation directly from GitHub source-code
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:25.1.0"
    compile 'com.github.denzilferreira:aware-client:development-SNAPSHOT'
//    compile 'com.android.support:customtabs:25.1.0'
    compile 'com.github.scribejava:scribejava-apis:3.3.0'
    compile 'net.danlew:android.joda:2.9.5.1'
}
